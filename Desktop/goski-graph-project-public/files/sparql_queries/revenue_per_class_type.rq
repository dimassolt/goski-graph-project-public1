PREFIX ski: <http://example.org/ski#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?normalizedLabel (SUM(xsd:decimal(?amountPaidOnline)) AS ?totalEarned)
WHERE {
  ?appointment a ski:Appointment ;
               ski:hasClassType ?classType ;
               ski:hasPayment ?payment .
  ?classType ski:className ?classTypeLabel .
  ?payment ski:amountPaidOnline ?amountPaidOnline .

  BIND(
    IF(CONTAINS(LCASE(STR(?classTypeLabel)), "classic"), "Classic Group",
    IF(CONTAINS(LCASE(STR(?classTypeLabel)), "skating"), "Skating Group",
    IF(CONTAINS(LCASE(STR(?classTypeLabel)), "nordic"), "Nordic Experience",
    IF(CONTAINS(LCASE(STR(?classTypeLabel)), "private"), "Private", "Other"))))
    AS ?normalizedLabel
  )
}
GROUP BY ?normalizedLabel
ORDER BY DESC(?totalEarned)
